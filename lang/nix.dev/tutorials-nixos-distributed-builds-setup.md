---
url: https://nix.dev/tutorials/nixos/distributed-builds-setup
title: Setting up distributed builds — nix.dev  documentation
source_domain: nix.dev
---

# Setting up distributed builds — nix.dev  documentation

[Skip to main content](https://nix.dev/tutorials/nixos/distributed-builds-setup#main-content)

[![](https://nix.dev/_static/img/nix.svg)
nix.dev](https://nix.dev/)

Official documentation for getting things done with Nix.

nix.dev as [PDF](https://nix.dev/nix-dev.pdf)

* [.md](https://nix.dev/_sources/tutorials/nixos/distributed-builds-setup.md "Download source file")

# Setting up distributed builds

# Setting up distributed builds[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#setting-up-distributed-builds "Link to this heading")

Nix can speed up builds by spreading the work across multiple computers at once.

## Introduction[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#introduction "Link to this heading")

In this tutorial, you’ll set up a separate build machine and configure your local machine to offload builds to it.

### What will you learn?[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#what-will-you-learn "Link to this heading")

You’ll learn how to

* Create a new user for remote build access from a local machine to the remote builder
* Configure remote builders with a sustainable setup
* Test remote builder connectivity and authentication
* Configure the local machine to automatically distribute builds

### What do you need?[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#what-do-you-need "Link to this heading")

* Familiarity with the [Nix language](https://nix.dev/tutorials/nix-language#reading-nix-language)
* Familiarity with the [Module system](https://nix.dev/tutorials/module-system/#module-system-tutorial)
* A *local machine* (example hostname: `localmachine`)

  The computer [with Nix installed](https://nix.dev/install-nix#install-nix) that distributes builds to other machines.
* A *remote machine* (example hostname: `remotemachine`)

  A computer running NixOS that accepts build jobs from the *local machine*.
  Follow [Provisioning remote machines via SSH](https://nix.dev/tutorials/nixos/provisioning-remote-machines#provisioning-remote-machines-tutorial) to set up a remote NixOS system.

### How long will it take?[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#how-long-will-it-take "Link to this heading")

* 25 minutes

## Create an SSH key pair[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#create-an-ssh-key-pair "Link to this heading")

The *local machine*’s Nix daemon runs as the `root` user and will need the *private* key file to authenticate itself to remote machines.
The *remote machine* will need the *public* key to recognize the *local machine*.

On the *local machine*, run the following command as `root` to create an SSH key pair:

```
# ssh-keygen -f /root/.ssh/remotebuild
```

Note

The name and location of the key pair files can be freely chosen.

## Set up the remote builder[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#set-up-the-remote-builder "Link to this heading")

In the NixOS configuration directory of the *remote machine*, create the file `remote-builder.nix`:

```
{
  users.users.remotebuild = {
    isSystemUser = true;
    group = "remotebuild";
    useDefaultShell = true;

    openssh.authorizedKeys.keyFiles = [ ./remotebuild.pub ];
  };

  users.groups.remotebuild = {};

  nix.settings.trusted-users = [ "remotebuild" ];
}
```

Copy the file `remotebuild.pub` into this directory.

This configuration module creates a new user `remotebuild` with no home directory.
The `root` user on the *local machine* will be able to log into the remote builder via SSH using the previously generated SSH key.

Add the new NixOS module to the existing configuration of the *remote machine*:

```
{
  imports = [
    ./remote-builder.nix
  ];

  # ...
}
```

Activate the new configuration as root:

```
nixos-rebuild switch --no-flake --target-host root@remotemachine
```

### Test authentication[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#test-authentication "Link to this heading")

Make sure that the SSH connection and authentication work.
On the *local machine*, run as `root`:

```
# ssh remotebuild@remotemachine -i /root/.ssh/remotebuild "echo hello"
hello
```

If the `hello` message is visible, the authentication works.

This test login also adds the host key of the remote builder to the `/root/.ssh/known_hosts` file of the local machine.
Future logins will not be interrupted by host key checks.

## Set up distributed builds[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#set-up-distributed-builds "Link to this heading")

Note

If your *local machine* runs NixOS, skip this section and [configure Nix through module options](https://nix.dev/tutorials/nixos/distributed-builds-setup#distributed-builds-config-nixos).

Configure Nix to use the remote builder by adding to the [Nix configuration file](https://nix.dev/manual/nix/2.23/command-ref/conf-file) as `root`:

```
# cat << EOF >> /etc/nix/nix.conf
builders = ssh-ng://remotebuild@remotebuilder $(nix-instantiate --eval -E builtins.currentSystem) /root/.ssh/remotemachine - - nixos-test,big-parallel,kvm
builders-use-substitutes = true
```

Detailed explanation

The first line registers the remote machine as a remote builder by specifying

* The protocol, user, and hostname
* The *local machine*’s [system type](https://nix.dev/manual/nix/2.23/command-ref/conf-file#conf-system)

  This will delegate jobs for that system type to the *remote machine*.
* The location of the SSH key
* A list of [supported system features](https://nix.dev/manual/nix/2.23/command-ref/conf-file#conf-system-features)

  This particular list must be specified in order to delegate building compilers and running [NixOS VM tests](https://nix.dev/tutorials/nixos/integration-testing-using-virtual-machines#integration-testing-vms) to remote machines.

See the [reference documentation on the `builders` setting](https://nix.dev/manual/nix/2.23/command-ref/conf-file#conf-builders) for details.

The second line instructs all remote builders to obtain dependencies from its own binary caches instead of from the *local machine*.
This assumes that the remote builders’ internet connection is at least as fast as the local machine’s internet connection.

To activate this configuration, restart the Nix daemon:

Linux

On Linux with `systemd`, run as `root`:

```
# systemctl restart nix-daemon.service
```

macOS

On macOS, run as `root`:

```
# sudo launchctl stop org.nixos.nix-daemon
# sudo launchctl start org.nixos.nix-daemon
```

NixOS

If your *local machine* runs NixOS, in its configuration directory create the file `distributed-builds.nix`:

```
{ pkgs, ... }:
{
  nix.distributedBuilds = true;
  nix.settings.builders-use-substitutes = true;

  nix.buildMachines = [
    {
      hostName = "remotebuilder";
      sshUser = "remotebuild";
      sshKey = "/root/.ssh/remotebuild";
      system = pkgs.stdenv.hostPlatform.system;
      supportedFeatures = [ "nixos-test" "big-parallel" "kvm" ];
    }
  ];
}
```

Detailed explanation

This configuration module enables distributed builds and adds the remote builder, specifying:

* The SSH hostname and username
* The location of the SSH key
* Which *local machine*’s [system type](https://nix.dev/manual/nix/2.23/command-ref/conf-file#conf-system)

  This will delegate jobs for that system type to the *remote machine*.
* A list of [supported system features](https://nix.dev/manual/nix/2.23/command-ref/conf-file#conf-system-features)

  This particular list must be specified in order to delegate building compilers and running [NixOS VM tests](https://nix.dev/tutorials/nixos/integration-testing-using-virtual-machines#integration-testing-vms) to remote machines.

See the [NixOS option documentation on `nix.buildMachines`](https://search.nixos.org/options?query=nix.buildMachines) for details.

The `builders-use-substitutes` instructs all remote builders to obtain dependencies from its own binary caches instead of from the *local machine*.
This assumes that the remote builders’ internet connection is at least as fast as the local machine’s internet connection.

Add the new NixOS module to the existing machine configuration:

```
{
  imports = [
    ./distributed-builds.nix
  ];

  # ...
}
```

Activate the new configuration as `root`:

```
# nixos-rebuild switch
```

## Test distributed builds[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#test-distributed-builds "Link to this heading")

Try building a new derivation on the *local machine*:

```
$ nix-build --max-jobs 0 -E "$(cat << EOF
(import <nixpkgs> {}).writeText "test" "$(date)"
EOF
)"
this derivation will be built:
  /nix/store/9csjdxv6ir8ccnjl6ijs36izswjgchn0-test.drv
building '/nix/store/9csjdxv6ir8ccnjl6ijs36izswjgchn0-test.drv' on 'ssh://remotebuilder'...
copying 0 paths...
copying 1 paths...
copying path '/nix/store/hvj5vyg4723nly1qh5a8daifbi1yisb3-test' from 'ssh://remotebuilder'...
/nix/store/hvj5vyg4723nly1qh5a8daifbi1yisb3-test
```

The resulting derivation changes on every invocation because it depends on the current system time, and thus can never be in the local cache.
The [`--max-jobs 0` command line argument](https://nix.dev/manual/nix/2.23/command-ref/conf-file#conf-max-jobs) forces Nix to build it on the remote builder.

The last output line contains the output path and indicates that build distribution works as expected.

## Optimise the remote builder configuration[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#optimise-the-remote-builder-configuration "Link to this heading")

To maximise parallelism, enable automatic garbage collection, and prevent Nix builds from consuming all memory, add the following lines to your `remote-builder.nix` configuration module:

```
 {
   users.users.remotebuild = {
     isNormalUser = true;
     createHome = false;
     group = "remotebuild";

     openssh.authorizedKeys.keyFiles = [ ./remotebuild.pub ];
   };

   users.groups.remotebuild = {};

-  nix.settings.trusted-users = [ "remotebuild" ];
+  nix = {
+    nrBuildUsers = 64;
+    settings = {
+      trusted-users = [ "remotebuild" ];
+
+      min-free = 10 * 1024 * 1024;
+      max-free = 200 * 1024 * 1024;

+      max-jobs = "auto";
+      cores = 0;
+    };
+  };

+  systemd.services.nix-daemon.serviceConfig = {
+    MemoryAccounting = true;
+    MemoryMax = "90%";
+    OOMScoreAdjust = 500;
+  };
}
```

Tip

Refer to the [Nix reference manual](https://nix.dev/manual/nix/2.23/command-ref/conf-file) for details on the options available in [`nix.settings`](https://search.nixos.org/options?show=nix.settings).

Remote builders can have different performance characteristics.
For each `nix.buildMachines` item, set the `maxJobs`, `speedFactor`, and `supportedFeatures` attributes correctly for each different remote builder.
This helps Nix on the *local machine* distributing builds the optimal way.

Tip

Refer to the [NixOS option documentation on `nix.buildMachines`](https://search.nixos.org/options?query=nix.buildMachines) for details.

Set the `nix.buildMachines.*.publicHostKey` field to each remote builder’s public host key to secure build distribution against man-in-the-middle scenarios.

## Next steps[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#next-steps "Link to this heading")

* [Configure Nix to use a custom binary cache](https://nix.dev/guides/recipes/add-binary-cache#custom-binary-cache) on each remote builder
* [Setting up post-build hooks](https://nix.dev/guides/recipes/post-build-hook#post-build-hooks) to upload store objects to a binary cache

To set up multiple builders, repeat the instructions in the [Set up the remote builder](https://nix.dev/tutorials/nixos/distributed-builds-setup#set-up-remote-builder) section for each remote builder.
Add all new remote builders to the `nix.buildMachines` attribute shown in the [Set up distributed builds](https://nix.dev/tutorials/nixos/distributed-builds-setup#set-up-distributed-builds) section.

## Alternatives[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#alternatives "Link to this heading")

* [nixbuild.net](https://nixbuild.net) - Nix remote builders as a service
* [Hercules CI](https://hercules-ci.com/) - Continuous integration with automatic build distribution
* [garnix](https://garnix.io/) - Hosted continuous integration with build distribution

## References[#](https://nix.dev/tutorials/nixos/distributed-builds-setup#references "Link to this heading")

* [Nix reference manual: Settings for distributed builds](https://nix.dev/manual/nix/latest/command-ref/conf-file#conf-builders)

Contents